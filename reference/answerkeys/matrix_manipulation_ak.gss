
/*******************************************
** Creating matrices
*********************************************/

// Create a 1x6 row vector
a = { 1.1 2.2 3.3 4.4 5.5 6.6 };

// Create a 3x2 matrix
b = { 1.1 2.1, 
      3.1 4.1, 
      5.1 6.1 };

// Create a 3x1 column vector
c = { 7, 8, 9 };

print a;
print b;
print c;

/*******************************************
** Scalar values
*********************************************/

d = { 3.14 };
d = 3.14;

/*******************************************
** Creating matrices with functions
*********************************************/

// Create a 4x3 matrix of ones
e = ones(4,3);

// Create a 6x2 matrix of zeros
e = zeros(6,2);

// Create a 100x4 matrix of random normal numbers N ~ (0, 1)
g = rndn(100, 4);

// Create a column vector: 1, 3, 5, 7, 9
h = seqa(1, 2, 5);

// Create a column vector: 1, 2, 4, 8, 16
i = seqm(1, 2, 5);

// Create a 4x4 identity matrix
j = eye(4);

/*******************************************
** Getting the size of matrices
*********************************************/

// Create a 270x11 matrix with uniform random numbers between 0 and 1
k = rndu(270, 11);

print rows(k);
print cols(k);
print getOrders(k);

/*******************************************
** Matrix concatenation
*********************************************/

// Horizontal matrix concatenation
m = ones(3, 1);
n = { 4 5,
      6 7,
      8 9 };
p = m ~ n;
print p;

// Vertical matrix concatenation
q = eye(2);
r = { 4 5,
      6 7,
      8 9 };
s = q | r;
print s;

// Combined vertical and horizontal concatenation
t = (1|2) ~ (3|4);
print t;
